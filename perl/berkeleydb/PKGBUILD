pkgname="carbonio-perl-berkeleydb"
pkgver="0.65"
pkgrel="2"
pkgdesc="Perl5 Module Supporting LDAP API"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "carbonio-bdb"
  "perl"
)
depends__yum=(
  "carbonio-bdb"
  "perl"
  "perl-core"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/P/PM/PMQS/BerkeleyDB-${pkgver}.tar.gz"
)
sha256sums=('410aa89e7232941d49106c9e048d6337475543ec1d233ea7cdb725cb5b963504')

build() {
  cd "${srcdir}/BerkeleyDB-${pkgver}"
  export BERKELEYDB_LIB="/opt/zextras/common/lib"
  export BERKELEYDB_INCLUDE="/opt/zextras/common/include"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5
  LD_RUN_PATH=/opt/zextras/common/lib make
}

package() {
  cd "${srcdir}/BerkeleyDB-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
