pkgname="carbonio-policyd"
pkgver="2.1.0.0"
pkgrel="1"
pkgdesc="Multi-platform policy server for popular MTAs (aka ClueBringer)"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
depends__apt=(
  "carbonio-perl-cache-fastmmap"
  "carbonio-perl-config-inifiles"
  "carbonio-perl-dbd-sqlite"
  "carbonio-perl-dbi"
  "carbonio-perl-mail-spf"
  "carbonio-perl-net-cidr"
  "carbonio-perl-net-dns"
  "carbonio-perl-net-server"
  "carbonio-perl-timedate"
  "libsocket6-perl"
  "perl"
)
depends__yum=(
  "carbonio-perl-cache-fastmmap"
  "carbonio-perl-config-inifiles"
  "carbonio-perl-dbd-sqlite"
  "carbonio-perl-dbi"
  "carbonio-perl-mail-spf"
  "carbonio-perl-net-cidr"
  "carbonio-perl-net-dns"
  "carbonio-perl-net-server"
  "carbonio-perl-timedate"
  "perl-core"
  "perl-Socket6"
  "perl"
)
makedepends=(
  "git"
)
section="libs"
priority="important"
source=(
  "git+https://gitlab.linux.community/policyd/policyd"
  "carbonio-policyd.patch"
)
sha256sums=('SKIP'
  '6fe53e6077c95a45f6fd5adffa8369b3f6fc0c1debe85c78240d4e45d483a6c6')

package() {
  cd "${srcdir}/policyd"
  git reset --hard cf484f1b93cf9965886cc89d33bdeee9c26426cb

  ./update-git-modules
  rm -rf .git awitpt/.git
  rm -rf awitpt/CPAN* awitpt/doxygen awitpt/SOAP
  mv -f awitpt/awitpt/db awitpt/
  mv -f awitpt/awitpt/cache.pm awitpt/
  mv -f awitpt/awitpt/netip.pm awitpt/
  rm -rf awitpt/awitpt
  rm -rf debian

  # carbonio custom patch for policyd
  patch -Np1 -i "${srcdir}/carbonio-policyd.patch"

  # executables
  mkdir -p "${pkgdir}/opt/zextras/common/bin"
  cp cbpadmin "${pkgdir}/opt/zextras/common/bin"
  cp cbpolicyd "${pkgdir}/opt/zextras/common/bin"

  # libs
  mkdir -p "${pkgdir}/opt/zextras/common/lib/policyd-2.1"
  cp -r cbp "${pkgdir}/opt/zextras/common/lib/policyd-2.1"
  cp -r awitpt "${pkgdir}/opt/zextras/common/lib/policyd-2.1"

  mkdir -p "${pkgdir}/opt/zextras/common/share"
  cp -r contrib "${pkgdir}/opt/zextras/common/share"
  cp -r database "${pkgdir}/opt/zextras/common/share"
  cp -r webui "${pkgdir}/opt/zextras/common/share"

}

postinst:apt() {
  if [ "$1" = "configure" ]; then
    # creating zextras group if it isn't already there
    if ! getent group zextras >/dev/null; then
      addgroup --system --force-badname --quiet zextras
    fi
    # creating zextras user if it isn't already there
    if ! getent passwd zextras >/dev/null; then
      adduser --system --force-badname --quiet \
        --ingroup zextras \
        --home /opt/zextras \
        --shell /bin/bash \
        zextras
      usermod -c "zextras user" zextras
    fi
  fi

  mkdir -p /opt/zextras/data/cbpolicyd
  chown -R zextras:zextras /opt/zextras/data/cbpolicyd
}

postinst:yum() {
  # creating zextras group if it isn't already there
  if ! getent group zextras >/dev/null; then
    groupadd --system zextras >/dev/null
  fi
  # creating zextras user if it isn't already there
  if ! getent passwd zextras >/dev/null; then
    useradd --system \
      -g zextras \
      -d /opt/zextras \
      --shell /bin/bash \
      zextras >/dev/null
    usermod -c "zextras user" zextras
  fi

  mkdir -p /opt/zextras/data/cbpolicyd
  chown -R zextras:zextras /opt/zextras/data/cbpolicyd
}
