pkgname="carbonio-policyd"
pkgver="2.1.0.0"
pkgrel="5"
pkgdesc="Multi-platform policy server for popular MTAs (aka ClueBringer)"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('GPL-2.0-or-later')
depends__apt=(
  "carbonio-perl-cache-fastmmap"
  "carbonio-perl-config-inifiles"
  "carbonio-perl-dbd-sqlite"
  "carbonio-perl-dbi"
  "carbonio-perl-mail-spf"
  "carbonio-perl-net-cidr"
  "carbonio-perl-net-dns"
  "carbonio-perl-net-server"
  "carbonio-perl-timedate"
  "libsocket6-perl"
  "perl"
)
depends__yum=(
  "carbonio-perl-cache-fastmmap"
  "carbonio-perl-config-inifiles"
  "carbonio-perl-dbd-sqlite"
  "carbonio-perl-dbi"
  "carbonio-perl-mail-spf"
  "carbonio-perl-net-cidr"
  "carbonio-perl-net-dns"
  "carbonio-perl-net-server"
  "carbonio-perl-timedate"
  "perl-core"
  "perl-Socket6"
  "perl"
)
makedepends=(
  "git"
)
section="libs"
priority="important"
source=(
  "git+https://gitlab.linux.community/policyd/policyd"
  "${pkgname}.patch"
  "${pkgname}.service"
  "systemd-tmpfile.conf"
  "${pkgname}.path"
  "${pkgname}-reload.service"
)
sha256sums=(
  'SKIP'
  '2f71d4274585f8ae963e2e2b081a0b0d0780e9eb7aa0e44cd99b5b7f3271119f'
  '6e23f9c46e73530260f45944c5409c3fd76e7e7e06680e90eed78f603a377669'
  '10734c3340cdc7cf75606449ebe3ab7a5779950d6d61d1e7adfca3b511d32efa'
  'a1ac5262f4e4595bc5f1f56d1974f3446db5895781f80682de64c77afbbe9887'
  'abd0d83064f61ded0d8ae6df9b6b4d2a660a87d2ec81d3d473c0e522c7d3be42'
)

package() {
  cd "${srcdir}/policyd"
  git reset --hard cf484f1b93cf9965886cc89d33bdeee9c26426cb

  ./update-git-modules
  rm -rf .git awitpt/.git
  rm -rf awitpt/CPAN* awitpt/doxygen awitpt/SOAP
  mv -f awitpt/awitpt/db awitpt/
  mv -f awitpt/awitpt/cache.pm awitpt/
  mv -f awitpt/awitpt/netip.pm awitpt/
  rm -rf awitpt/awitpt
  rm -rf debian

  # carbonio custom patch for policyd
  patch -Np1 -i "${srcdir}/carbonio-policyd.patch"

  # executables
  mkdir -p "${pkgdir}/opt/zextras/common/bin"
  cp cbpadmin "${pkgdir}/opt/zextras/common/bin"
  cp cbpolicyd "${pkgdir}/opt/zextras/common/bin"

  # libs
  mkdir -p "${pkgdir}/opt/zextras/common/lib/policyd-2.1"
  cp -r cbp "${pkgdir}/opt/zextras/common/lib/policyd-2.1"
  cp -r awitpt "${pkgdir}/opt/zextras/common/lib/policyd-2.1"

  mkdir -p "${pkgdir}/opt/zextras/common/share"
  cp -r contrib "${pkgdir}/opt/zextras/common/share"
  cp -r database "${pkgdir}/opt/zextras/common/share"
  cp -r webui "${pkgdir}/opt/zextras/common/share"

  # systemd units
  install -D -m 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -D -m 644 "${srcdir}/${pkgname}-reload.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-reload.service"
  install -D -m 644 "${srcdir}/${pkgname}.path" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.path"

  # systemd tmpfiles.d configuration
  install -D -m 644 "${srcdir}/systemd-tmpfile.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}conf"
}

postinst__apt() {
  if [ "$1" = "configure" ]; then
    # creating zextras group if it isn't already there
    if ! getent group zextras >/dev/null; then
      addgroup --system --force-badname --quiet zextras
    fi
    # creating zextras user if it isn't already there
    if ! getent passwd zextras >/dev/null; then
      adduser --system --force-badname --quiet \
        --ingroup zextras \
        --home /opt/zextras \
        --shell /bin/bash \
        zextras
      usermod -c "zextras user" zextras
    fi
  fi

  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-policyd.conf || :
}

postinst__yum() {
  # creating zextras group if it isn't already there
  if ! getent group zextras >/dev/null; then
    groupadd --system zextras >/dev/null
  fi
  # creating zextras user if it isn't already there
  if ! getent passwd zextras >/dev/null; then
    useradd --system \
      -g zextras \
      -d /opt/zextras \
      --shell /bin/bash \
      zextras >/dev/null
    usermod -c "zextras user" zextras
  fi

  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-policyd.conf || :
}
