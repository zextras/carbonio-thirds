pkgname="carbonio-amavisd"
pkgver="2.14.0" # renovate: datasource=gitlab-tags depName=amavis/amavis
pkgrel="1"
pkgdesc="High-performance interface between mailer (MTA) and content checkers"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('GPL-2.0-or-later')
depends=(
  "carbonio-perl-archive-zip"
  "carbonio-perl-berkeleydb"
  "carbonio-perl-convert-tnef"
  "carbonio-perl-convert-uulib"
  "carbonio-perl-digest-sha1"
  "carbonio-perl-file-libmagic"
  "carbonio-perl-http-message"
  "carbonio-perl-io-socket-ip"
  "carbonio-perl-io-stringy"
  "carbonio-perl-ip-country"
  "carbonio-perl-libwww"
  "carbonio-perl-lwp-protocol-https"
  "carbonio-perl-mail-dkim"
  "carbonio-perl-mailtools"
  "carbonio-perl-mime-tools"
  "carbonio-perl-net-libidn2"
  "carbonio-perl-net-server"
  "carbonio-perl-net-ssleay"
  "carbonio-perl-unix-syslog"
)
optdepends__apt=(
  "arc"
  "arj"
  "libgeo-ip-perl"
  "libgeo-ipfree-perl"
  "libnet-patricia-perl"
  "lrzip"
  "lzop"
  "nomarch"
  "p7zip-full"
  "p7zip-rar"
  "rar"
  "tnef"
)
optdepends__yum=(
  "arj"
  "cabextract"
  "lzop"
  "nomarch"
  "p7zip"
  "p7zip-plugins"
  "pax"
  "unar"
  "unzoo"
)
section="utils"
priority="optional"
source=(
  "https://gitlab.com/amavis/amavis/-/archive/v${pkgver}/amavis-v${pkgver}.tar.bz2"
  "carbonio-mailthreat.service"
  "carbonio-mailthreat-update.service"
  "carbonio-mailthreat-update.timer"
  "socket_and_misc.patch"
  "systemd-tmpfile.conf"
)
sha256sums=(
  '6973ee582b193479ccb0e7d375ad208051236ee0318a17071d263397a2823189'
  '883b33fdf848149c119d5554c974aa322097a23fb729aaa7260aca563bbda53f'
  '2aae564745031515e80899115fb6f13ad0be86056a504b2c03cbd785980e7cf5'
  'c80d0a99da02ab4058ad4f5538c5905cc212b962e43446e2b678d665892fb1bf'
  '8466a63fcf6d1b6a52db0a2e2c22efdd23610615a1ae9d6c5eed00ee0a1e917b'
  '19bb2416f8afa0ed357018e245f24ddfd5c9ed23013f3b62a0ce53bec3156e1b'
)

_package() {
  cd "${srcdir}/amavis-v${pkgver}"

  patch -Np1 -i "${srcdir}/socket_and_misc.patch"

  install -D -p -m755 bin/amavisd \
    "${pkgdir}/opt/zextras/common/sbin/amavisd"

  install -p -m755 bin/amavisd-{agent,nanny,release,signer,status,submit} \
    "${pkgdir}/opt/zextras/common/sbin"

  install -p -m755 bin/amavis-{mc,services} \
    "${pkgdir}/opt/zextras/common/sbin"

  mkdir -p "${pkgdir}/opt/zextras/common/lib/"
  cp -pr lib "${pkgdir}/opt/zextras/common/lib/perl5"

  # systemd tmpfiles.d configuration
  install -Dm644 "${srcdir}/systemd-tmpfile.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/carbonio-mailthreat.conf"
}

_package_systemd() {
  # systemd units
  install -Dm644 "${srcdir}/carbonio-mailthreat.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-mailthreat.service"
  install -Dm644 "${srcdir}/carbonio-mailthreat-update.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-mailthreat-update.service"
  install -Dm644 "${srcdir}/carbonio-mailthreat-update.timer" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-mailthreat-update.timer"
}

_postinst() {
  # creating zextras group if it isn't already there
  getent group zextras >/dev/null \
    || groupadd -r zextras
  # creating zextras user if it isn't already there
  getent passwd zextras >/dev/null \
    || useradd -r -g zextras -d /opt/zextras -s /bin/bash -c "zextras user" zextras
}

_postinst_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
    systemctl enable carbonio-mailthreat.service &>/dev/null || :
    systemctl enable carbonio-mailthreat-update.service &>/dev/null || :
    systemctl enable carbonio-mailthreat-update.timer &>/dev/null || :
  fi
}

_prerm_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable --now carbonio-mailthreat.service &>/dev/null || :
    systemctl --no-reload disable --now carbonio-mailthreat-update.service &>/dev/null || :
    systemctl --no-reload disable --now carbonio-mailthreat-update.timer &>/dev/null || :
  fi
}

package() {
  _package
  _package_systemd
}

package__rocky_8() {
  _package
}

package__rocky_9() {
  _package
  _package_systemd
}

package__ubuntu_jammy() {
  _package
}

package__ubuntu_noble() {
  _package
  _package_systemd
}

postinst__ubuntu_jammy() {
  if [ "$1" = "configure" ]; then
    _postinst
  fi

  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-mailthreat.conf || :

  # Clean up temporary amavis work directories
  if [ -d /opt/zextras/data/amavisd/tmp ]; then
    find /opt/zextras/data/amavisd/tmp -maxdepth 1 -type d -name 'amavis-*' -exec rm -rf {} \; >/dev/null 2>&1
  fi
}

postinst__ubuntu_noble() {
  if [ "$1" = "configure" ]; then
    _postinst
  fi

  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-mailthreat.conf || :

  # Clean up temporary amavis work directories
  if [ -d /opt/zextras/data/amavisd/tmp ]; then
    find /opt/zextras/data/amavisd/tmp -maxdepth 1 -type d -name 'amavis-*' -exec rm -rf {} \; >/dev/null 2>&1
  fi

  _postinst_systemd
}

postinst__rocky_8() {
  _postinst

  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-mailthreat.conf || :

  # Clean up temporary amavis work directories
  if [ -d /opt/zextras/data/amavisd/tmp ]; then
    find /opt/zextras/data/amavisd/tmp -maxdepth 1 -type d -name 'amavis-*' -exec rm -rf {} \; >/dev/null 2>&1
  fi
}

postinst__rocky_9() {
  _postinst

  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-mailthreat.conf || :

  # Clean up temporary amavis work directories
  if [ -d /opt/zextras/data/amavisd/tmp ]; then
    find /opt/zextras/data/amavisd/tmp -maxdepth 1 -type d -name 'amavis-*' -exec rm -rf {} \; >/dev/null 2>&1
  fi

  _postinst_systemd
}

prerm__ubuntu_noble() {
  _prerm_systemd
}

prerm__rocky_9() {
  _prerm_systemd
}

postrm__ubuntu_noble() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
  fi
}

postrm__rocky_9() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
  fi
}
