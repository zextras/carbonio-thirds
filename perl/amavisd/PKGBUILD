pkgname="carbonio-amavisd"
pkgver="2.13.0"
pkgrel="2"
pkgdesc="High-performance interface between mailer (MTA) and content checkers"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends=(
  "carbonio-perl-archive-zip"
  "carbonio-perl-berkeleydb"
  "carbonio-perl-convert-tnef"
  "carbonio-perl-convert-uulib"
  "carbonio-perl-digest-sha1"
  "carbonio-perl-file-libmagic"
  "carbonio-perl-http-message"
  "carbonio-perl-io-socket-inet6"
  "carbonio-perl-io-socket-ip"
  "carbonio-perl-io-stringy"
  "carbonio-perl-ip-country"
  "carbonio-perl-libwww"
  "carbonio-perl-lwp-protocol-https"
  "carbonio-perl-mail-dkim"
  "carbonio-perl-mailtools"
  "carbonio-perl-mime-tools"
  "carbonio-perl-net-libidn2"
  "carbonio-perl-net-server"
  "carbonio-perl-net-ssleay"
  "carbonio-perl-unix-syslog"
)
section="utils"
priority="optional"
source=(
  "https://gitlab.com/amavis/amavis/-/archive/v${pkgver}/amavis-v${pkgver}.tar.bz2"
  "carbonio-antispam.service"
  "carbonio-antispam-db.service"
  "socket_and_misc.patch"
)
sha256sums=('155447e049d02e5170b9183ad3867684514fcb37852440c4679ba6d7cc56e50d'
  'a5639238fce07b03cccbab4fdb646b60bd8f45e375b90bd699fb6e64561c9dec'
  '3b2a7a0218539e6e1d87952ed8368b71022096b63edcf1d1d4ae8f94d29e8f4e'
  'be3f0d56552ce674365b9fb37f90eb185c261c202f976e8a42b3a49ee9eb306d')

package() {
  cd "${srcdir}/amavis-v${pkgver}"

  patch -Np1 -i "${srcdir}/socket_and_misc.patch"

  install -D -p -m755 bin/amavisd \
    "${pkgdir}/opt/zextras/common/sbin/amavisd"

  install -p -m755 bin/amavisd-{agent,nanny,release,signer,status,submit} \
    "${pkgdir}/opt/zextras/common/sbin"

  install -p -m755 bin/amavis-{mc,services} \
    "${pkgdir}/opt/zextras/common/sbin"

  mkdir -p "${pkgdir}/opt/zextras/common/lib/"
  cp -pr lib "${pkgdir}/opt/zextras/common/lib/perl5"

  # systemd units
  install -D -m 644 "${srcdir}/carbonio-antispam.service" \
    "${pkgdir}/lib/systemd/system/carbonio-antispam.service"
  install -D -m 644 "${srcdir}/carbonio-antispam-db.service" \
    "${pkgdir}/lib/systemd/system/carbonio-antispam-db.service"
}
