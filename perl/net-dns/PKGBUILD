pkgname="carbonio-perl-net-dns"
pkgver="1.53" # renovate: datasource=cpan depName=Net::DNS
pkgrel="2"
pkgdesc="Perl Interface to the Domain Name System"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "carbonio-perl-digest-hmac"
  "carbonio-perl-io-socket-ip"
  "carbonio-perl-net-libidn2"
  "perl"
)
depends__yum=(
  "carbonio-perl-digest-hmac"
  "carbonio-perl-io-socket-ip"
  "carbonio-perl-net-libidn2"
  "perl-core"
  "perl"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-${pkgver}.tar.gz"
  "remote-tests.patch"
)
sha256sums=('39f4b82ffe822f4d28691f6c4260f6f1fe6b5a09b546b56dd2410c21b38c1380'
  '485fd51f739315a559ddcea7c6defcf38ee61f153b3a3fb8e3863a5860380774')

build() {
  cd "${srcdir}/Net-DNS-${pkgver}"
  patch -Np1 -i ../remote-tests.patch

  perl -I /opt/zextras/common/lib/perl5 Makefile.PL \
    --noonline-tests \
    INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  LD_RUN_PATH=/opt/zextras/common/lib make
}

package() {
  cd "${srcdir}/Net-DNS-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
