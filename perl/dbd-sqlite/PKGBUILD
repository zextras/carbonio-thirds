pkgname="carbonio-perl-dbd-sqlite"
pkgver="1.66"
pkgrel="2"
pkgdesc="Self-contained RDBMS in a DBI Driver"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
depends__apt=(
  "carbonio-perl-dbi"
  "perl"
)
depends__yum=(
  "carbonio-perl-dbi"
  "perl-core"
  "perl"
)
makedepends__apt=(
  "libsqlite3-dev"
)
makedepends__yum=(
  "sqlite-devel"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-${pkgver}.tar.gz"
)
sha256sums=('89e75dff9f66c273e0e459d51f151f88960eb7d82e2821e6902f18688ba592fe')

build() {
  cd "${srcdir}/DBD-SQLite-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  make
}

package() {
  cd "${srcdir}/DBD-SQLite-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
