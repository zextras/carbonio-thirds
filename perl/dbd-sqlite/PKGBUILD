pkgname="carbonio-perl-dbd-sqlite"
pkgver="1.78" # renovate: datasource=cpan depName=DBD::SQLite
pkgrel="1"
pkgdesc="Self-contained RDBMS in a DBI Driver"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=("Artistic-1.0-Perl OR GPL-1.0-or-later")
depends__apt=(
  "carbonio-perl-dbi"
  "perl"
)
depends__yum=(
  "carbonio-perl-dbi"
  "perl-core"
  "perl"
)
makedepends__apt=(
  "libsqlite3-dev"
)
makedepends__yum=(
  "sqlite-devel"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-${pkgver}.tar.gz"
)
sha256sums=('efbad7794bafaa4e7476c07445a33bbfe1040e380baa3395a02635eebe3859d5')

build() {
  cd "${srcdir}/DBD-SQLite-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  make
}

package() {
  cd "${srcdir}/DBD-SQLite-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
