pkgname="carbonio-perl-net-ssleay"
pkgver="1.94" # renovate: datasource=cpan depName=Net::SSLeay
pkgrel="3"
pkgdesc="Perl bindings for OpenSSL"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=("Artistic-2.0")
depends__apt=(
  "carbonio-openssl"
  "perl"
)
depends__yum=(
  "carbonio-openssl"
  "perl-core"
  "perl"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/C/CH/CHRISN/Net-SSLeay-${pkgver}.tar.gz"
)
sha256sums=('9d7be8a56d1bedda05c425306cc504ba134307e0c09bda4a788c98744ebcd95d')

build() {
  cd "${srcdir}/Net-SSLeay-${pkgver}"
  export OPENSSL_PREFIX="/opt/zextras/common"

  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  LD_RUN_PATH=/opt/zextras/common/lib make
}

package() {
  cd "${srcdir}/Net-SSLeay-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
