pkgname="carbonio-perl-net-server"
pkgver="2.014" # renovate: datasource=cpan depName=Net::Server
pkgrel="2"
pkgdesc="Extensible, general Perl server engine"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "carbonio-perl-socket"
  "perl"
)
depends__yum=(
  "carbonio-perl-socket"
  "perl-core"
  "perl"
)
makedepends__apt=(
  "libmodule-install-perl"
)
makedepends__yum=(
  "perl-Module-Install"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/R/RH/RHANDOM/Net-Server-${pkgver}.tar.gz"
)
sha256sums=('3406b9ca5a662a0075eed47fb78de1316b601c94f62a0ee34a5544db9baa3720')

build() {
  cd "${srcdir}/Net-Server-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  make
}

package() {
  cd "${srcdir}/Net-Server-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
