pkgname="carbonio-perl-crypt-openssl-rsa"
pkgver="0.37" # renovate: datasource=cpan depName=Crypt::OpenSSL::RSA
pkgrel="1"
pkgdesc="RSA encoding and decoding, using the openSSL libraries"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('Artistic-1.0-Perl OR GPL-1.0-or-later')
depends__apt=(
  "carbonio-perl-crypt-openssl-random"
  "carbonio-openssl"
  "perl"
)
depends__yum=(
  "carbonio-perl-crypt-openssl-random"
  "carbonio-openssl"
  "perl-core"
  "perl"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/T/TO/TODDR/Crypt-OpenSSL-RSA-${pkgver}.tar.gz"
  "remove-guess.patch"
)
sha256sums=('917f7312532f8f4af4f3acbf6ba10e0151f8577d2ef1f38e1035229be86eb6f4'
            '5bb9aaaa694c365cbe9bb79915f7a37c8224858948bc40d98911747b8ec7fd45')

prepare() {
  cd "${srcdir}/Crypt-OpenSSL-RSA-${pkgver}"
  patch -Np1 -i "${srcdir}/remove-guess.patch"
}

build() {
  cd "${srcdir}/Crypt-OpenSSL-RSA-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  LD_RUN_PATH=/opt/zextras/common/lib make
}

package() {
  cd "${srcdir}/Crypt-OpenSSL-RSA-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
