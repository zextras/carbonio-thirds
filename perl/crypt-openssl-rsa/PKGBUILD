pkgname="carbonio-perl-crypt-openssl-rsa"
pkgver="0.33"
pkgrel="1"
pkgdesc="RSA encoding and decoding, using the openSSL libraries"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "carbonio-perl-crypt-openssl-random"
  "carbonio-openssl"
  "perl"
)
depends__yum=(
  "carbonio-perl-crypt-openssl-random"
  "carbonio-openssl"
  "perl-core"
  "perl"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/T/TO/TODDR/Crypt-OpenSSL-RSA-${pkgver}.tar.gz"
  "remove-guess.patch"
)
sha256sums=('bdbe630f6d6f540325746ad99977272ac8664ff81bd19f0adaba6d6f45efd864'
  '03613db116d70d730a9aeab688abb96f78fd232d052376e4b96a828b1af8c50e')

build() {
  cd "${srcdir}/Crypt-OpenSSL-RSA-${pkgver}"
  patch -Np1 -i "${srcdir}/remove-guess.patch"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  LD_RUN_PATH=/opt/zextras/common/lib make
}

package() {
  cd "${srcdir}/Crypt-OpenSSL-RSA-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
