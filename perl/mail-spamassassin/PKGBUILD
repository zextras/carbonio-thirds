pkgname="carbonio-perl-mail-spamassassin"
pkgver="4.0.1"
pkgrel="1"
pkgdesc="Spam detector and markup engine"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends=(
  "carbonio-openssl"
  "carbonio-perl-http-message"
  "carbonio-perl-io-socket-inet6"
  "carbonio-perl-io-socket-ip"
  "carbonio-perl-io-socket-ssl"
  "carbonio-perl-ip-country"
  "carbonio-perl-libwww"
  "carbonio-perl-mail-dkim"
  "carbonio-perl-mail-spf"
  "carbonio-perl-net-dns"
  "carbonio-perl-net-http"
  "pyzor"
  "arj"
  "lzop"
  "nomarch"
)
# arc tnef only on Ubuntu
depends__apt=(
  "arc"
  "lrzip"
  "tnef"
  "rar"
  "p7zip-rar"
  "libgeoip1"
  "p7zip-full"
  "p7zip"
  "libnet-patricia-perl"
  "libnet-cidr-lite-perl"
  "libgeo-ipfree-perl"
  "libgeo-ip-perl"
  "geoip-database"
  "razor"
)

depends__yum=(
	"epel-release"
	"unar"
	"p7zip"
	"p7zip-plugins"
	"lz4-devel"
	"perl-Razor-Agent"
)
provides__yum=(
  "perl(BSD::Resource)"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/S/SI/SIDNEY/Mail-SpamAssassin-${pkgver}.tar.gz"
)
sha256sums=('5c6bb222e18405f1a276816d04e1ffc5cc90785e1265714b4506c2b541d6d5e5')

build() {
  cd "${srcdir}/Mail-SpamAssassin-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE="/opt/zextras/common" \
    INSTALLSITEMAN1DIR="/opt/zextras/common/share/man/man1" INSTALLSITEMAN3DIR="/opt/zextras/common/share/man/man3" \
    INSTALLMAN3DIR="/opt/zextras/common/share/man/man1" INSTALLVENDORMAN3DIR="/opt/zextras/common/share/man/man1" \
    INSTALLMAN3DIR="/opt/zextras/common/share/man/man3" INSTALLVENDORMAN3DIR="/opt/zextras/common/share/man/man3" \
    LIB="/opt/zextras/common/lib/perl5" DATADIR="/opt/zextras/data/spamassassin/rules" \
    CONFDIR="/opt/zextras/data/spamassassin/localrules" \
    DATADIR="/opt/zextras/data/spamassassin/rules" \
    LOCALSTATEDIR="/opt/zextras/data/spamassassin/state" \
    CONTACT_ADDRESS=""
  PERL5LIB="/opt/zextras/common/lib/perl5" \
    make
}

package() {
  cd "${srcdir}/Mail-SpamAssassin-${pkgver}"
  PERL5LIB="/opt/zextras/common/lib/perl5" \
    make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
  rm -rf "${pkgdir}"/opt/zextras/common/data/spamassassin/rules \
    "${pkgdir}"/opt/zextras/data/spamassassin/rules/*

  sed -i -e 's/^#loadplugin Mail::SpamAssassin::Plugin::DCC/loadplugin Mail::SpamAssassin::Plugin::DCC/' \
    "${pkgdir}"/opt/zextras/data/spamassassin/localrules/v310.pre
  sed -i -e 's/^#loadplugin Mail::SpamAssassin::Plugin::Rule2XSBody/loadplugin Mail::SpamAssassin::Plugin::Rule2XSBody/' \
    "${pkgdir}"/opt/zextras/data/spamassassin/localrules/v320.pre
}
