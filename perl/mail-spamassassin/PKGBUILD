pkgname="carbonio-perl-mail-spamassassin"
pkgver="4.0.2" # renovate: datasource=cpan depName=Mail::SpamAssassin
pkgrel="1"
pkgdesc="Spam detector and markup engine"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends=(
  "carbonio-openssl"
  "carbonio-perl-http-message"
  "carbonio-perl-io-socket-ip"
  "carbonio-perl-io-socket-ssl"
  "carbonio-perl-ip-country"
  "carbonio-perl-libwww"
  "carbonio-perl-mail-dkim"
  "carbonio-perl-mail-spf"
  "carbonio-perl-net-dns"
  "carbonio-perl-net-http"
  "make"
  "re2c"
)
optdepends__apt=(
  "arc"
  "arj"
  "carbonio-perl-net-cidr-lite"
  "geoip-database"
  "libgeoip1"
  "libgeo-ipfree-perl"
  "libgeo-ip-perl"
  "lrzip"
  "lzop"
  "nomarch"
  "p7zip-rar"
  "pyzor"
  "rar"
  "razor"
  "tnef"
)
optdepends__yum=(
  "arj"
  "cabextract"
  "lz4"
  "lzop"
  "nomarch"
  "p7zip"
  "p7zip-plugins"
  "perl-Razor-Agent"
  "pyzor"
  "unar"
  "unzoo"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/G/GB/GBECHIS/Mail-SpamAssassin-${pkgver}.tar.gz"
)
sha256sums=('c521be978cef3d49b1e139477ca60a0bd498345fc98274796e44161fae49a17f')

build() {
  cd "${srcdir}/Mail-SpamAssassin-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE="/opt/zextras/common" \
    INSTALLSITEMAN1DIR="/opt/zextras/common/share/man/man1" INSTALLSITEMAN3DIR="/opt/zextras/common/share/man/man3" \
    INSTALLMAN3DIR="/opt/zextras/common/share/man/man1" INSTALLVENDORMAN3DIR="/opt/zextras/common/share/man/man1" \
    INSTALLMAN3DIR="/opt/zextras/common/share/man/man3" INSTALLVENDORMAN3DIR="/opt/zextras/common/share/man/man3" \
    LIB="/opt/zextras/common/lib/perl5" DATADIR="/opt/zextras/data/spamassassin/rules" \
    CONFDIR="/opt/zextras/data/spamassassin/localrules" \
    DATADIR="/opt/zextras/data/spamassassin/rules" \
    LOCALSTATEDIR="/opt/zextras/data/spamassassin/state" \
    CONTACT_ADDRESS=""
  PERL5LIB="/opt/zextras/common/lib/perl5" \
    make
}

package() {
  cd "${srcdir}/Mail-SpamAssassin-${pkgver}"
  PERL5LIB="/opt/zextras/common/lib/perl5" \
    make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
  rm -rf "${pkgdir}"/opt/zextras/common/data/spamassassin/rules \
    "${pkgdir}"/opt/zextras/data/spamassassin/rules/*

  sed -i -e 's/^#loadplugin Mail::SpamAssassin::Plugin::DCC/loadplugin Mail::SpamAssassin::Plugin::DCC/' \
    "${pkgdir}"/opt/zextras/data/spamassassin/localrules/v310.pre
  sed -i -e 's/^#loadplugin Mail::SpamAssassin::Plugin::Rule2XSBody/loadplugin Mail::SpamAssassin::Plugin::Rule2XSBody/' \
    "${pkgdir}"/opt/zextras/data/spamassassin/localrules/v320.pre
}
