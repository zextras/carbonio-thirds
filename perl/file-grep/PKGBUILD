pkgname="carbonio-perl-file-grep"
pkgver="0.02" # renovate: datasource=cpan depName=File::Grep
pkgrel="3"
pkgdesc="Find matches to a pattern in a series of files and related functions"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=("Artistic-1.0-Perl OR GPL-1.0-or-later")
depends__apt=(
  "perl"
)
depends__yum=(
  "perl-core"
  "perl"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/M/MN/MNEYLON/File-Grep-${pkgver}.tar.gz"
)
sha256sums=('462e15274eb6278521407ea302d9eea7252cd44cab2382871f7de833d5f85632')

build() {
  cd "${srcdir}/File-Grep-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  make
}

package() {
  cd "${srcdir}/File-Grep-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
