pkgname="carbonio-perl-libwww"
pkgver="6.81" # renovate: datasource=cpan depName=LWP
pkgrel="1"
pkgdesc="The World-Wide Web library for Perl"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=("Artistic-1.0-Perl OR GPL-1.0-or-later")
depends__apt=(
  "carbonio-perl-encode-locale"
  "carbonio-perl-file-listing"
  "carbonio-perl-html-parser"
  "carbonio-perl-http-cookies"
  "carbonio-perl-http-daemon"
  "carbonio-perl-http-date"
  "carbonio-perl-http-negotiate"
  "carbonio-perl-lwp-mediatypes"
  "carbonio-perl-net-http"
  "carbonio-perl-uri"
  "carbonio-perl-www-robotrules"
  "libtry-tiny-perl"
  "perl"
)
depends__yum=(
  "carbonio-perl-encode-locale"
  "carbonio-perl-file-listing"
  "carbonio-perl-html-parser"
  "carbonio-perl-http-cookies"
  "carbonio-perl-http-daemon"
  "carbonio-perl-http-date"
  "carbonio-perl-http-negotiate"
  "carbonio-perl-lwp-mediatypes"
  "carbonio-perl-net-http"
  "carbonio-perl-uri"
  "carbonio-perl-www-robotrules"
  "perl-core"
  "perl-Try-Tiny"
  "perl"
)
section="libs"
priority="important"
source=(
  "https://cpan.metacpan.org/authors/id/O/OA/OALDERS/libwww-perl-${pkgver}.tar.gz"
)
sha256sums=('ab30552f194e8b5ae3ac0885132fd1d4ea04c4c7fe6555765b98f01af70c1736')

build() {
  cd "${srcdir}/libwww-perl-${pkgver}"
  perl -I /opt/zextras/common/lib/perl5 Makefile.PL INSTALL_BASE=/opt/zextras/common \
    INSTALLSITEMAN1DIR=/opt/zextras/common/share/man/man1 INSTALLSITEMAN3DIR=/opt/zextras/common/share/man/man3 \
    LIB=/opt/zextras/common/lib/perl5 LIBS="-L/opt/zextras/common/lib -lssl -lcrypto" INC="-I/opt/zextras/common/include"
  make
}

package() {
  cd "${srcdir}/libwww-perl-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
