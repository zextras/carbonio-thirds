pkgname="carbonio-libbsd"
pkgver="0.10.0" # renovate: datasource=github-tags depName=guillemj/libbsd
pkgrel="3"
pkgdesc="Provides useful functions commonly found on BSD systems like strlcpy()"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('BSD-2-Clause AND BSD-3-Clause AND Beerware AND ISC AND MIT AND LicenseRef-public-domain')
depends__apt=(
  "libc6"
)
depends__yum=(
  "glibc"
)
section="libs"
priority="important"
source=(
  "https://github.com/guillemj/libbsd/archive/refs/tags/${pkgver}.tar.gz"
)
sha256sums=('00d0a39eb5334cae476be30027a63370cf9427242af66a70505699ffc83451d7')

build() {
  cd "${srcdir}/libbsd-${pkgver}"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export CFLAGS="-fPIC -D_REENTRANT"
  export CXXFLAGS="-fPIC -D_REENTRANT"

  echo "${pkgver}" >.dist-version
  autoreconf -fiv
  ./configure \
    --prefix=/opt/zextras/common \
    --disable-static \
    --disable-tests
  make -j16
}

package() {
  cd "${srcdir}/libbsd-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.a
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
  rm -rf "${pkgdir}"/opt/zextras/common/share/man/
}
