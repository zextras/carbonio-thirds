pkgname="carbonio-openssl"
pkgver="1.1.1v"
pkgrel="1"
pkgdesc="Zextras's Secure Socket Layer build"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
makedepends__apt=(
  "perl"
)
makedepends__yum=(
  "perl"
)
section="libs"
priority="important"
source=(
  "https://www.openssl.org/source/openssl-${pkgver}.tar.gz"
)
sha256sums=('d6697e2871e77238460402e9362d47d18382b15ef9f246aba6c7bd780d38a6b0')

build() {
  cd "${srcdir}/openssl-${pkgver}"
  ./Configure no-idea enable-ec_nistp_64_gcc_128 no-mdc2 no-rc5 no-ssl2 \
    no-hw \
    no-tests \
    shared \
    no-ssl3-method \
    linux-x86_64 \
    -DOPENSSL_NO_HEARTBEATS \
    --prefix=/opt/zextras/common \
    --libdir=lib \
    --openssldir=/opt/zextras/common/etc/ssl
  LD_RUN_PATH=/opt/zextras/common/lib make depend
  LD_RUN_PATH=/opt/zextras/common/lib make all
}

package() {
  cd "${srcdir}/openssl-${pkgver}"
  LD_RUN_PATH=/opt/zextras/common/lib \
    MANDIR="/opt/zextras/common/share/man" \
    LIBS="" \
    make DESTDIR="${pkgdir}" install_sw install_ssldirs
  rm "${pkgdir}"/opt/zextras/common/lib/*.a
  rm "${pkgdir}"/opt/zextras/common/etc/ssl/misc/tsget*
  # rm -rf "${pkgdir}"/opt/zextras/common/include
}
