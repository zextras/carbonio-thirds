pkgname="carbonio-openssl"
pkgver="3.0.16" # renovate: datasource=github-tags depName=openssl/openssl
pkgrel="1"
pkgdesc="Zextras's Secure Socket Layer build"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('Apache-2.0')
makedepends__apt=(
  "perl"
)
makedepends__yum=(
  "perl"
)
section="libs"
priority="important"
source=(
  "https://github.com/openssl/openssl/releases/download/openssl-${pkgver}/openssl-${pkgver}.tar.gz"
)
sha256sums=('57e03c50feab5d31b152af2b764f10379aecd8ee92f16c985983ce4a99f7ef86')

build() {
  cd "${srcdir}/openssl-${pkgver}"
  ./Configure \
    --libdir=lib \
    --openssldir=/opt/zextras/common/etc/ssl \
    --prefix=/opt/zextras/common \
    --with-rand-seed=devrandom,rdcpu,os,getrandom \
    -DOPENSSL_NO_HEARTBEATS \
    enable-ec_nistp_64_gcc_128 \
    linux-x86_64 \
    no-idea \
    no-mdc2 no-rc5 \
    no-ssl3-method \
    no-tests \
    shared
  LD_RUN_PATH=/opt/zextras/common/lib make depend
  LD_RUN_PATH=/opt/zextras/common/lib make build_sw
}

package() {
  cd "${srcdir}/openssl-${pkgver}"
  LD_RUN_PATH=/opt/zextras/common/lib \
    MANDIR="/opt/zextras/common/share/man" \
    LIBS="" \
    make DESTDIR="${pkgdir}" install_sw install_ssldirs

  rm "${pkgdir}"/opt/zextras/common/lib/*.a
  rm "${pkgdir}"/opt/zextras/common/etc/ssl/misc/tsget*
  cp "${pkgdir}/opt/zextras/common/etc/ssl/openssl.cnf" \
    "${pkgdir}/opt/zextras/common/etc/ssl/openssl-source.cnf"

  # Til now, these symlinks are only required for carbonio-docs-editor
  install -d "${pkgdir}/opt/zextras/common/lib64"
  ln -s /opt/zextras/common/lib/libcrypto.so.3 \
    "${pkgdir}/opt/zextras/common/lib64/libcrypto.so.3"
  ln -s /opt/zextras/common/lib/libssl.so.3 \
    "${pkgdir}/opt/zextras/common/lib64/libssl.so.3"
}
