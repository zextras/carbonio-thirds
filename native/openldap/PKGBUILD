pkgname="carbonio-openldap"
pkgver="2.6.10" # renovate: datasource=github-tags depName=openldap/openldap  extractVersion=^OPENLDAP_REL_ENG_(?<version>.+)$ versioning=regex:^(?<major>\d+)_(?<minor>\d+)(_(?<patch>\d+))?$
pkgrel="2"
pkgdesc="Lightweight Directory Access Protocol (LDAP) library"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('OLDAP-2.8')
depends__apt=(
  "carbonio-cyrus-sasl"
  "carbonio-libsodium"
  "carbonio-openssl"
  "libcap2-bin"
  "libltdl7"
)
depends__yum=(
  "carbonio-cyrus-sasl"
  "carbonio-libsodium"
  "carbonio-openssl"
  "libcap"
  "libtool-ltdl"
)
makedepends__apt=(
  "libcap2-bin"
)
makedepends__yum=(
  "libcap"
)
section="libs"
priority="important"
source=(
  "https://www.openldap.org/software/download/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
  "${pkgname}.patch"
  "${pkgname}.service"
  "systemd-tmpfile.conf"
)
sha256sums=(
  'c065f04aad42737aebd60b2fe4939704ac844266bc0aeaa1609f0cad987be516'
  'a2ba7ee70d6caf8f21bdac376d95b1e98361874c4fcc62dd63356f9f2a1cae99'
  'ea5035b346caa69b14d15b2fb205838164a86a752c1d9239a483f059183d8150'
  'd64b270fd9863319863cd9fb5619d4386cfdb87d841d11a7e5dc4fcb558c1689'
)

_package() {
  cd "${srcdir}/openldap-${pkgver}"

  make install DESTDIR="${pkgdir}" STRIP=""

  make -C libraries/liblmdb install prefix=/opt/zextras/common DESTDIR="${pkgdir}"
  make -C contrib/slapd-modules/noopsrch install prefix=/opt/zextras/common DESTDIR="${pkgdir}"
  make -C contrib/slapd-modules/passwd/sha2 install prefix=/opt/zextras/common DESTDIR="${pkgdir}"

  rm -rf "${pkgdir}/run"

  # tmpfiles.d configuration
  install -Dm644 "${srcdir}/systemd-tmpfile.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  # libexec .a and .la files should not be deleted
  # they are related to slapd plugins
  find "${pkgdir}/opt/zextras/common/lib" -name '*.a' -delete
  find "${pkgdir}/opt/zextras/common/lib" -name '*.la' -delete
}

_package_systemd() {
  # systemd unit
  install -Dm644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}

_postinst() {
  # Set permissions and capabilities on slapd binary
  # Note: On fresh install, zextras group may not exist yet (created by carbonio-core).
  # In that case, carbonio-core's postinst will set these permissions.
  # On upgrade/reinstall, zextras exists and we can set permissions here.
  if getent group zextras >/dev/null 2>&1; then
    # Note: chmod resets capabilities, so setcap must run after chmod
    chown root:zextras /opt/zextras/common/libexec/slapd
    chmod 750 /opt/zextras/common/libexec/slapd
    setcap CAP_NET_BIND_SERVICE=+ep /opt/zextras/common/libexec/slapd
  fi

  # Apply tmpfiles.d configuration
  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-openldap.conf >/dev/null 2>&1 || :
}

_postinst_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
    systemctl enable carbonio-openldap.service &>/dev/null || :
  fi
}

_prerm_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable --now carbonio-openldap.service &>/dev/null || :
  fi
}

build() {
  cd "${srcdir}/openldap-${pkgver}"
  patch -Np1 -i "${srcdir}/carbonio-openldap.patch"

  export LDFLAGS="-L/opt/zextras/common/lib -Wl,-rpath,/opt/zextras/common/lib"
  export CFLAGS="-D_REENTRANT"
  export CPPFLAGS="-I/opt/zextras/common/include"
  export PATH="/opt/zextras/common/bin:/usr/bin"

  ./configure \
    --prefix=/opt/zextras/common \
    --localstatedir=/run/carbonio/ \
    --disable-ndb \
    --disable-perl \
    --disable-sql \
    --disable-wt \
    --enable-argon2 \
    --enable-backends=mod \
    --enable-crypt \
    --enable-dynamic \
    --enable-ipv6 \
    --enable-local \
    --enable-modules \
    --enable-overlays=mod \
    --enable-slapd \
    --enable-spasswd \
    --with-cyrus-sasl \
    --with-threads \
    --with-tls=openssl

  LD_RUN_PATH=/opt/zextras/common/lib make depend
  LD_RUN_PATH=/opt/zextras/common/lib make DEFINES="-DCHECK_CSN -DSLAP_SCHEMA_EXPOSE" -C libraries
  LD_RUN_PATH=/opt/zextras/common/lib make DEFINES="-DCHECK_CSN -DSLAP_SCHEMA_EXPOSE" -C servers
  LD_RUN_PATH=/opt/zextras/common/lib make DEFINES="-DCHECK_CSN -DSLAP_SCHEMA_EXPOSE" -C clients
  make -C libraries/liblmdb prefix=/opt/zextras/common
  make -C contrib/slapd-modules/noopsrch prefix=/opt/zextras/common
  make -C contrib/slapd-modules/passwd/sha2 prefix=/opt/zextras/common
}

package() {
  _package
  _package_systemd
}

package__rocky_8() {
  _package
}

package__rocky_9() {
  _package
  _package_systemd
}

package__ubuntu_jammy() {
  _package
}

package__ubuntu_noble() {
  _package
  _package_systemd
}

postinst__ubuntu_jammy() {
  _postinst
}

postinst__ubuntu_noble() {
  _postinst
  _postinst_systemd
}

postinst__rocky_8() {
  _postinst
}

postinst__rocky_9() {
  _postinst
  _postinst_systemd
}

prerm__rocky_9() {
  _prerm_systemd
}

prerm__ubuntu_noble() {
  _prerm_systemd
}

postrm__rocky_9() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}

postrm__ubuntu_noble() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}
