pkgname="carbonio-postfix"
pkgver="3.10.5" # renovate: datasource=repology depName=arch/postfix
pkgrel="2"
pkgdesc="Fast, easy to administer, secure mail server"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('IPL-1.0 OR EPL-2.0')
depends__apt=(
  "carbonio-cyrus-sasl"
  "carbonio-mariadb"
  "carbonio-openldap"
  "carbonio-openssl"
  "libc6"
  "libpcre3"
)
depends__ubuntu_noble=(
  "carbonio-cyrus-sasl"
  "carbonio-mariadb"
  "carbonio-openldap"
  "carbonio-openssl"
  "libc6"
  "libnsl2"
  "libpcre3"
)
makedepends__apt=(
  "libc6-dev"
  "libpcre3-dev"
)
makedepends__ubuntu_noble=(
  "libc6-dev"
  "libnsl-dev"
  "libpcre3-dev"
)
depends__yum=(
  "carbonio-cyrus-sasl"
  "carbonio-mariadb>10.1.48"
  "carbonio-openldap"
  "carbonio-openssl"
  "libnsl2"
  "pcre"
)
makedepends__yum=(
  "libnsl2-devel"
  "pcre-devel"
)
section="utils"
priority="optional"
source=(
  "http://ftp.netclusive.de/pub/postfix/postfix-release/official/postfix-${pkgver}.tar.gz"
  "${pkgname}.patch"
  "${pkgname}.service"
  "systemd-sysuser.conf"
  "systemd-tmpfile.conf"
)
sha256sums=(
  '6a926bf702173861b08e49bcb51fca3a2f269f9a337f72ef159bf46052087e35'
  '82f5ae29a6667aff05211acbb82667d77994ccce6fdeab89893d1356e9b796bd'
  '16775c587d70cf4f4f2e885bd4e209c8d97ac03d75ac69a6b9d3fc4b58ee8366'
  '6ad170eece16c7ee4d947a84cf2efb153a4ed52ace02c70016829253dd70aa5e'
  'edbd7857db1140e9995ab9d210797308c7b826035d06adde7d0d62755e8eb602'
)

_package() {
  cd "${srcdir}/postfix-${pkgver}"
  sh postfix-install -non-interactive-package install_root="${pkgdir}"
  install -Dm755 auxiliary/qshape/qshape.pl \
    "${pkgdir}"/opt/zextras/common/sbin/qshape.pl

  # systemd sysusers.d configuration
  install -Dm644 "${srcdir}/systemd-sysuser.conf" \
    "${pkgdir}/usr/lib/sysusers.d/carbonio-postfix.conf"

  # systemd tmpfiles.d configuration
  install -Dm644 "${srcdir}/systemd-tmpfile.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  cd "${pkgdir}"/opt/zextras/common/sbin
  rm -f newaliases
  rm -f mailq
  ln -s sendmail mailq
  ln -s sendmail newaliases
}

_package_systemd() {
  # systemd unit
  install -Dm644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}

_postinst_apt() {
  if [ "$1" = "configure" ]; then
    # Create postfix users and groups via sysusers.d
    systemd-sysusers /usr/lib/sysusers.d/carbonio-postfix.conf >/dev/null 2>&1 || :

    # Create postfix directories and set SGID on binaries via tmpfiles.d
    systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-postfix.conf >/dev/null 2>&1 || :
  fi
}

_postinst_yum() {
  # Create postfix users and groups via sysusers.d
  systemd-sysusers /usr/lib/sysusers.d/carbonio-postfix.conf >/dev/null 2>&1 || :

  # Create postfix directories and set SGID on binaries via tmpfiles.d
  systemd-tmpfiles --create /usr/lib/tmpfiles.d/carbonio-postfix.conf >/dev/null 2>&1 || :
}

_postinst_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
    systemctl enable carbonio-postfix.service &>/dev/null || :
  fi
}

_prerm_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable --now carbonio-postfix.service &>/dev/null || :
  fi
}

build() {
  cd "${srcdir}/postfix-${pkgver}"
  patch -Np1 -i "${srcdir}/carbonio-postfix.patch"

  make makefiles \
    command_directory=/opt/zextras/common/sbin \
    daemon_directory=/opt/zextras/common/libexec \
    config_directory=/opt/zextras/common/conf \
    queue_directory=/opt/zextras/data/postfix/spool \
    sendmail_path=/opt/zextras/common/sbin/sendmail \
    newalias_path=/opt/zextras/common/sbin/newaliases \
    mailq_path=/opt/zextras/common/sbin/mailq \
    manpage_directory=/opt/zextras/common/share/man \
    html_directory=no \
    readme_directory=no \
    default_database_type=lmdb \
    data_directory=/opt/zextras/data/postfix/data \
    CCARGS='\
  -DHAS_LDAP \
  -DHAS_MYSQL \
  -DNO_DB \
  -DUSE_CYRUS_SASL \
  -DHAS_LMDB \
  -DUSE_LDAP_SASL \
  -DUSE_SASL_AUTH \
  -DUSE_TLS \
  -DHAS_PCRE \
  -I/opt/zextras/common/include \
  -I/opt/zextras/common/include/mysql \
  -I/opt/zextras/common/include/sasl \
  -I/usr/include \
  -I/usr/include/pcre' \
    AUXLIBS='\
  -L/opt/zextras/common/lib \
	-lpcre -lldap -llber -llmdb -lmysqlclient -lsasl2 -lssl -lcrypto \
	-L/usr/lib -lz -lm -lpthread' \
    SHLIB_RPATH='\
  -Wl,-rpath,/opt/zextras/common/lib/postfix \
  -Wl,-rpath,/opt/zextras/common/lib \
  -L/opt/zextras/common/lib' \
    OPT='\
  -Wl,-rpath,/opt/zextras/common/lib \
  -L/opt/zextras/common/lib'

  make -j16
}

package() {
  _package
  _package_systemd
}

package__rocky_8() {
  _package
}

package__rocky_9() {
  _package
  _package_systemd
}

package__ubuntu_jammy() {
  _package
}

package__ubuntu_noble() {
  _package
  _package_systemd
}

postinst__ubuntu_jammy() {
  _postinst_apt "$@"
}

postinst__ubuntu_noble() {
  _postinst_apt "$@"
  _postinst_systemd
}

postinst__rocky_8() {
  _postinst_yum
}

postinst__rocky_9() {
  _postinst_yum
  _postinst_systemd
}

prerm__rocky_9() {
  _prerm_systemd
}

prerm__ubuntu_noble() {
  _prerm_systemd
}

postrm__rocky_9() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}

postrm__ubuntu_noble() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}
