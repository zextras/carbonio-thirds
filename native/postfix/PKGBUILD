pkgname="carbonio-postfix"
pkgver="3.8.6"
pkgrel="2"
pkgdesc="Fast, easy to administer, secure mail server"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "libc6"
  "libpcre3"
  "carbonio-openldap"
  "carbonio-cyrus-sasl"
  "carbonio-openssl"
  "carbonio-mariadb"
)
makedepends__apt=(
  "libc6-dev"
  "libpcre3-dev"
)
depends__yum=(
  "libnsl2"
  "pcre"
  "carbonio-openldap"
  "carbonio-cyrus-sasl"
  "carbonio-openssl"
  "carbonio-mariadb>10.1.48"
)
makedepends__yum=(
  "libnsl2-devel"
  "pcre-devel"
)
section="utils"
priority="optional"
source=(
  "https://de.postfix.org/ftpmirror/official/postfix-${pkgver}.tar.gz"
  "${pkgname}.patch"
  "${pkgname}.service"
)
sha256sums=('4b6e17c826cc438cc3016a9c0a55ea7e77c6cbafba7dd57241d81b690b0e9774'
  'd4cdba8dffe1a7e23c0495cec5caf18f5cb10b1f52db0bb866f2247e63635598'
  '8e30e3979abd88aa4b1c0f1d1811ada6c7da138ea05e1686a26fc7b8e7200405')

build() {
  cd "${srcdir}/postfix-${pkgver}"
  patch -Np1 -i "${srcdir}/carbonio-postfix.patch"

  make makefiles \
    command_directory=/opt/zextras/common/sbin \
    daemon_directory=/opt/zextras/common/libexec \
    config_directory=/opt/zextras/common/conf \
    queue_directory=/opt/zextras/data/postfix/spool \
    sendmail_path=/opt/zextras/common/sbin/sendmail \
    newalias_path=/opt/zextras/common/sbin/newaliases \
    mailq_path=/opt/zextras/common/sbin/mailq \
    manpage_directory=/opt/zextras/common/share/man \
    html_directory=no \
    readme_directory=no \
    default_database_type=lmdb \
    data_directory=/opt/zextras/data/postfix/data \
    CCARGS='\
  -DHAS_LDAP \
  -DHAS_MYSQL \
  -DNO_DB \
  -DUSE_CYRUS_SASL \
  -DHAS_LMDB \
  -DUSE_LDAP_SASL \
  -DUSE_SASL_AUTH \
  -DUSE_TLS \
  -DHAS_PCRE \
  -I/opt/zextras/common/include \
  -I/opt/zextras/common/include/mysql \
  -I/opt/zextras/common/include/sasl \
  -I/usr/include \
  -I/usr/include/pcre' \
    AUXLIBS='\
  -L/opt/zextras/common/lib \
	-lpcre -lldap -llber -llmdb -lmysqlclient -lsasl2 -lssl -lcrypto \
	-L/usr/lib -lz -lm -lpthread' \
    SHLIB_RPATH='\
  -Wl,-rpath,/opt/zextras/common/lib/postfix \
  -Wl,-rpath,/opt/zextras/common/lib \
  -L/opt/zextras/common/lib' \
    OPT='\
  -Wl,-rpath,/opt/zextras/common/lib \
  -L/opt/zextras/common/lib'

  make -j16
}

package() {
  cd "${srcdir}/postfix-${pkgver}"
  sh postfix-install -non-interactive-package install_root="${pkgdir}"
  install -Dm755 auxiliary/qshape/qshape.pl \
    "${pkgdir}"/opt/zextras/common/sbin/qshape.pl

  # systemd unit
  install -D -m 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/lib/systemd/system/${pkgname}.service"

  cd "${pkgdir}"/opt/zextras/common/sbin
  rm -f newaliases
  rm -f mailq
  ln -s sendmail mailq
  ln -s sendmail newaliases
}
