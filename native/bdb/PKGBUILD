pkgname="carbonio-bdb"
pkgver="5.3.28" # renovate: datasource=repology depName=arch/db
pkgrel="2"
pkgdesc="The Berkeley DB embedded database system"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('Sleepycat AND BSD-3-Clause AND CDDL-1.0')
section="utils"
priority="optional"
source=(
  "https://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz"
  "atomic.patch"
)
sha256sums=('e0a992d740709892e81f9d93f06daf305cf73fb81b545afe72478043172c3628'
  'ba0e2b4f53e9cb0ec58f60a979b53b8567b4565f0384886196f1fc1ef111d151')

build() {
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"
  cd "${srcdir}/db-${pkgver}"
  patch -p0 -i "${srcdir}/atomic.patch"

  cd build_unix

  ../dist/configure \
    --prefix=/opt/zextras/common \
    --enable-compat185 \
    --enable-shared \
    --disable-static \
    --enable-cxx \
    --enable-dbm \
    --enable-stl \
    --disable-test
  make LIBSO_LIBS=-lpthread
}

package() {
  cd "${srcdir}/db-${pkgver}"
  cd build_unix
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/opt/zextras/common/docs
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}
