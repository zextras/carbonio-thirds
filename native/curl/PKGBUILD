pkgname="carbonio-curl"
pkgver="7.79.1"
pkgrel="1"
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "carbonio-krb5"
  "carbonio-openssl"
  "zlib1g"
)
makedepends__apt=(
  "zlib1g-dev"
)
depends__yum=(
  "carbonio-krb5"
  "carbonio-openssl"
  "zlib"
)
makedepends__yum=(
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://curl.haxx.se/download/curl-${pkgver}.tar.gz"
)
sha256sums=('370b11201349816287fb0ccc995e420277fbfcaf76206e309b3f60f0eda090c2')

build() {
  cd "${srcdir}/curl-${pkgver}"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export CPPFLAGS="-I/opt/zextras/common/include"

  ./configure --prefix=/opt/zextras/common \
    --disable-ldap --disable-ldaps \
    --with-gssapi=/opt/zextras/common \
    --with-ssl=/opt/zextras/common \
    --without-gnutls \
    --with-ca-bundle=/opt/zextras/common/share/curl/ca-bundle.crt \
    --enable-ipv6 \
    --with-zlib \
    --without-libidn \
    --disable-static
  make -j16
}

package() {
  cd "${srcdir}/curl-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make ca-bundle
  mkdir -p "${pkgdir}/opt/zextras/common/share/curl"
  cp -f lib/ca-bundle.crt "${pkgdir}/opt/zextras/common/share/curl/ca-bundle.crt"
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}
