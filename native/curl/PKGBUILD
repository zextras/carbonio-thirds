pkgname="carbonio-curl"
pkgver="8.5.0"
pkgrel="1"
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "carbonio-krb5"
  "carbonio-openssl"
  "zlib1g"
)
makedepends__apt=(
  "zlib1g-dev"
)
depends__yum=(
  "carbonio-krb5"
  "carbonio-openssl"
  "zlib"
)
makedepends__yum=(
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://curl.haxx.se/download/curl-${pkgver}.tar.gz"
)
sha256sums=('05fc17ff25b793a437a0906e0484b82172a9f4de02be5ed447e0cab8c3475add')

build() {
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export CPPFLAGS="-I/opt/zextras/common/include"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"

  cd "${srcdir}/curl-${pkgver}"

  ./configure \
    --prefix=/opt/zextras/common \
    --disable-ldap \
    --disable-ldaps \
    --disable-static \
    --enable-ipv6 \
    --with-ca-bundle=/opt/zextras/common/share/curl/ca-bundle.crt \
    --with-gssapi=/opt/zextras/common \
    --with-openssl=/opt/zextras/common \
    --with-zlib \
    --without-gnutls \
    --without-libidn2
  make -j16
}

package() {
  cd "${srcdir}/curl-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make ca-bundle
  mkdir -p "${pkgdir}/opt/zextras/common/share/curl"
  cp -f lib/ca-bundle.crt "${pkgdir}/opt/zextras/common/share/curl/ca-bundle.crt"
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}
