pkgname="carbonio-curl"
pkgver="8.14.1" # renovate: datasource=github-releases depName=curl/curl
pkgrel="1"
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('curl')
depends__apt=(
  "carbonio-krb5"
  "carbonio-openssl"
  "libbrotli1"
  "zlib1g"
)
makedepends__apt=(
  "libbrotli-dev"
  "zlib1g-dev"
)
depends__yum=(
  "brotli"
  "carbonio-krb5"
  "carbonio-openssl"
  "zlib"
)
makedepends__yum=(
  "brotli-devel"
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://curl.haxx.se/download/curl-${pkgver}.tar.gz"
  "https://github.com/nss-dev/nss/raw/refs/heads/master/lib/ckfw/builtins/certdata.txt"
)
sha256sums=(
  '6766ada7101d292b42b8b15681120acd68effa4a9660935853cf6d61f0d984d4'
  'SKIP'
)

build() {
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export CPPFLAGS="-I/opt/zextras/common/include"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"

  cd "${srcdir}/curl-${pkgver}"

  ./configure \
    --prefix=/opt/zextras/common \
    --disable-ldap \
    --disable-ldaps \
    --disable-static \
    --enable-ipv6 \
    --with-ca-bundle=/opt/zextras/common/share/curl/ca-bundle.crt \
    --with-gssapi=/opt/zextras/common \
    --with-openssl=/opt/zextras/common \
    --with-zlib \
    --without-gnutls \
    --without-libidn2 \
    --without-libpsl
  make -j16
}

package() {
  cd "${srcdir}/curl-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # make ca-bundle target is replaced with the script.
  # certdata.txt is retrieved directly from github, cause of
  # hg.mozilla.org unreliability.
  # mk-ca-bundle.pl `-n` relies on an already existent certdata.txt
  cp "${srcdir}/certdata.txt" .
  scripts/mk-ca-bundle.pl -n -b -l -u lib/ca-bundle.crt

  mkdir -p "${pkgdir}/opt/zextras/common/share/curl"
  cp -f lib/ca-bundle.crt "${pkgdir}/opt/zextras/common/share/curl/ca-bundle.crt"
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}
