pkgname="carbonio-libxml2"
pkgver="2.9.12"
pkgrel="2"
pkgdesc="XML parsing library, version 2"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
depends__apt=(
  "carbonio-icu"
  "zlib1g"
)
depends__yum=(
  "carbonio-icu"
  "zlib"
)
makedepends__apt=(
  "autoconf"
  "pkg-config"
  "zlib1g-dev"
)
makedepends__yum=(
  "zlib-devel"
)
section="libs"
priority="important"
source=(
  "https://github.com/GNOME/libxml2/archive/refs/tags/v${pkgver}.tar.gz"
)
sha256sums=('8a4ddd706419c210b30b8978a51388937fd9362c34fc9a3d69e4fcc6f8055ee0')

build() {
  cd "${srcdir}/libxml2-${pkgver}"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export CFLAGS="-I/opt/zextras/common/include"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"

  autoreconf -fiv
  ./configure \
    --prefix=/opt/zextras/common \
    --enable-shared=yes \
    --enable-static=no \
    --without-python \
    --without-iconv \
    --with-icu
  make -j16
}

package() {
  cd "${srcdir}/libxml2-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}"/opt/zextras/common/bin/xml2-config
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
  rm -rf "${pkgdir}"/opt/zextras/common/share/man
  rm -rf "${pkgdir}"/opt/zextras/common/share/gtk-doc
}
