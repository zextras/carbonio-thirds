# renovate: datasource=github-tags depName=GNOME/libxml2
pkgname="carbonio-libxml2"
pkgver="2.10.1"
pkgrel="2"
pkgdesc="XML parsing library, version 2"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('MIT')
depends__apt=(
  "carbonio-icu"
  "zlib1g"
)
depends__yum=(
  "carbonio-icu"
  "zlib"
)
makedepends__apt=(
  "autoconf"
  "pkg-config"
  "zlib1g-dev"
)
makedepends__yum=(
  "zlib-devel"
)
section="libs"
priority="important"
source=(
  "https://github.com/GNOME/libxml2/archive/refs/tags/v${pkgver}.tar.gz"
)
sha256sums=('fb9f90ca7cceb1930d2a890c1c222ef709352b0fea6e5430d03902133f1d476c')

build() {
  cd "${srcdir}/libxml2-${pkgver}"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export CFLAGS="-I/opt/zextras/common/include"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"

  autoreconf -fiv
  ./configure \
    --prefix=/opt/zextras/common \
    --enable-shared=yes \
    --enable-static=no \
    --without-python \
    --without-iconv \
    --with-icu
  make -j16
}

package() {
  cd "${srcdir}/libxml2-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}"/opt/zextras/common/bin/xml2-config
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
  rm -rf "${pkgdir}"/opt/zextras/common/share/man
  rm -rf "${pkgdir}"/opt/zextras/common/share/gtk-doc
}
