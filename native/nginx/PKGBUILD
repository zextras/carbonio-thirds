pkgname="carbonio-nginx"
pkgver="1.24.0"
pkgrel="2"
pkgdesc="Lightweight HTTP server and IMAP/POP3 proxy server"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=("CUSTOM")
depends__apt=(
  "libbrotli1"
  "libpcre3"
  "zlib1g"
  "carbonio-cyrus-sasl"
  "carbonio-openssl"
)
makedepends__apt=(
  "git"
  "libbrotli-dev"
  "libpcre3-dev"
  "zlib1g-dev"
)
depends__yum=(
  "brotli"
  "pcre"
  "zlib"
  "carbonio-cyrus-sasl"
  "carbonio-openssl"
)
makedepends__yum=(
  "brotli-devel"
  "git"
  "pcre-devel"
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://nginx.org/download/nginx-${pkgver}.tar.gz"
  "nginx-tcp-keepalive::git+https://github.com/nviennot/nginx-tcp-keepalive#commit=19755fc0e22aa7d7ac2198f78b297951d31ea1dc"
  "nginx-brotli::git+https://github.com/google/ngx_brotli#commit=a71f9312c2deb28875acc7bacfdd5695a111aa53"
  "modules::git+https://github.com/zextras/carbonio-nginx-modules#commit=e62d730a1f5f23494fba7f0a45d3271c335fae6f"
  "carbonio-${pkgver}.patch"
  "${pkgname}.service"
  "CVE-2023-44487.patch"
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  '48df9423d3aca1e95f79ff3f89c063b983457451bbe0d6e8aac38acb7595adc5'
  'a922347182aa80001c303ee222015a388cded0a5be9505a90087b91011a4bbe1'
  '72ad36b212fffe7a4824fc37e53972f051050ace11ecc4c55c2c9bdf3ba575ac'
)

prepare() {
  cd "${srcdir}/nginx-brotli"
  git submodule update --init --recursive
}

build() {

  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib"
  export CFLAGS="-g -O2 -fdebug-prefix-map=/etc=. -fstack-protector-strong"

  cd "${srcdir}/nginx-${pkgver}"
  patch -Np1 -i ../"carbonio-${pkgver}.patch"
  patch -Np1 -i ../CVE-2023-44487.patch

  ./configure --prefix=/opt/zextras/common \
    --with-cc-opt="-g -I/opt/zextras/common/include" \
    --with-ld-opt="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib" \
    --with-http_ssl_module \
    --with-http_stub_status_module \
    --with-pcre \
    --with-http_addition_module \
    --with-http_auth_request_module \
    --with-http_degradation_module \
    --with-http_gunzip_module \
    --with-http_gzip_static_module \
    --with-http_secure_link_module \
    --with-http_slice_module \
    --with-http_ssl_module \
    --with-http_stub_status_module \
    --with-http_sub_module \
    --with-http_v2_module \
    --with-stream \
    --with-stream_ssl_module \
    --with-stream_ssl_preread_module \
    --with-threads \
    --with-mail \
    --with-mail-sasl \
    --with-mail_ssl_module \
    --error-log-path=/opt/zextras/log/nginx.log \
    --http-log-path=/opt/zextras/log/nginx.access.log \
    --http-client-body-temp-path=/opt/zextras/data/tmp/nginx/client \
    --http-proxy-temp-path=/opt/zextras/data/tmp/nginx/proxy \
    --http-fastcgi-temp-path=/opt/zextras/data/tmp/nginx/fastcgi \
    --without-http_scgi_module \
    --without-http_uwsgi_module \
    --add-module="${srcdir}/nginx-brotli" \
    --add-module="${srcdir}/nginx-tcp-keepalive" \
    --add-module="${srcdir}/modules/http/sso" \
    --add-module="${srcdir}/modules/http/upstreamzmauth" \
    --add-module="${srcdir}/modules/mail/zmauth" \
    --add-module="${srcdir}/modules/mail/throttle"
  make -j16
}

package() {
  cd "${srcdir}/nginx-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/opt/zextras/data/nginx
  mv "${pkgdir}"/opt/zextras/common/html "${pkgdir}"/opt/zextras/data/nginx/

  # systemd unit
  install -D -m 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/lib/systemd/system/${pkgname}.service"
}

postinst__apt() {
  if [ "$1" = "configure" ]; then
    # creating zextras group if it isn't already there
    if ! getent group zextras >/dev/null; then
      addgroup --system --force-badname --quiet zextras
    fi

    # creating zextras user if it isn't already there
    if ! getent passwd zextras >/dev/null; then
      adduser --system --force-badname --quiet \
        --ingroup zextras \
        --home /opt/zextras \
        --shell /bin/bash \
        zextras
      usermod -c "zextras user" zextras
    fi
  fi

  chown zextras:zextras /opt/zextras/log
}

postinst__yum() {
  # creating zextras group if it isn't already there
  if ! getent group zextras >/dev/null; then
    groupadd --system zextras >/dev/null
  fi
  # creating zextras user if it isn't already there
  if ! getent passwd zextras >/dev/null; then
    useradd --system \
      -g zextras \
      -d /opt/zextras \
      --shell /bin/bash \
      zextras >/dev/null
    usermod -c "zextras user" zextras
  fi

  chown zextras:zextras /opt/zextras/log
}
