pkgname="carbonio-nginx"
pkgver="1.26.3" # renovate: datasource=github-releases depName=nginx/nginx
pkgrel="1"
pkgdesc="Lightweight HTTP server and IMAP/POP3 proxy server"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=("CUSTOM")
depends__apt=(
  "carbonio-cyrus-sasl"
  "carbonio-openssl"
  "libbrotli1"
  "libcap2-bin"
  "libpcre3"
  "zlib1g"
)
makedepends__apt=(
  "git"
  "libbrotli-dev"
  "libcap2-bin"
  "libpcre3-dev"
  "zlib1g-dev"
)
depends__yum=(
  "carbonio-cyrus-sasl"
  "carbonio-openssl"
  "brotli"
  "libcap"
  "pcre"
  "zlib"
)
makedepends__yum=(
  "brotli-devel"
  "git"
  "libcap"
  "pcre-devel"
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://nginx.org/download/nginx-${pkgver}.tar.gz"
  "nginx-tcp-keepalive::git+https://github.com/nviennot/nginx-tcp-keepalive#commit=19755fc0e22aa7d7ac2198f78b297951d31ea1dc"
  "nginx-brotli::git+https://github.com/google/ngx_brotli#commit=a71f9312c2deb28875acc7bacfdd5695a111aa53"
  "modules::git+https://github.com/zextras/carbonio-nginx-modules#commit=e62d730a1f5f23494fba7f0a45d3271c335fae6f"
  "${pkgname}.service"
  "${pkgname}-${pkgver}.patch"
)
sha256sums=(
  '69ee2b237744036e61d24b836668aad3040dda461fe6f570f1787eab570c75aa'
  'SKIP'
  'SKIP'
  'SKIP'
  '49a203086ff15c647f9a05a6c10e008eea829ce05ed61a7ba5ccdb7f26ba243c'
  'e3a0888055505f9202860682978ae246b5f8dc4d48ced85216482432fefbb1c8'
)
prepare() {
  cd "${srcdir}/nginx-brotli"
  git submodule update --init --recursive
}

build() {

  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib"
  export CFLAGS="-g -O2 -fdebug-prefix-map=/etc=. -fstack-protector-strong"

  cd "${srcdir}/nginx-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}.patch"

  ./configure \
    --add-module="${srcdir}/nginx-brotli" \
    --add-module="${srcdir}/nginx-tcp-keepalive" \
    --add-module="${srcdir}/modules/http/sso" \
    --add-module="${srcdir}/modules/http/upstreamzmauth" \
    --add-module="${srcdir}/modules/mail/zmauth" \
    --add-module="${srcdir}/modules/mail/throttle" \
    --error-log-path=/opt/zextras/log/nginx.log \
    --http-log-path=/opt/zextras/log/nginx.access.log \
    --http-client-body-temp-path=/opt/zextras/data/tmp/nginx/client \
    --http-proxy-temp-path=/opt/zextras/data/tmp/nginx/proxy \
    --http-fastcgi-temp-path=/opt/zextras/data/tmp/nginx/fastcgi \
    --pid-path=/run/carbonio/nginx.pid \
    --prefix=/opt/zextras/common \
    --with-cc-opt="-g -I/opt/zextras/common/include" \
    --with-ld-opt="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib" \
    --with-http_ssl_module \
    --with-http_stub_status_module \
    --with-pcre \
    --with-http_addition_module \
    --with-http_auth_request_module \
    --with-http_degradation_module \
    --with-http_gunzip_module \
    --with-http_gzip_static_module \
    --with-http_secure_link_module \
    --with-http_slice_module \
    --with-http_ssl_module \
    --with-http_stub_status_module \
    --with-http_sub_module \
    --with-http_v2_module \
    --with-stream \
    --with-stream_ssl_module \
    --with-stream_ssl_preread_module \
    --with-threads \
    --with-mail \
    --with-mail-sasl \
    --with-mail_ssl_module \
    --without-http_scgi_module \
    --without-http_uwsgi_module
  make -j16
}

package() {
  cd "${srcdir}/nginx-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/opt/zextras/data/nginx
  mv "${pkgdir}"/opt/zextras/common/html "${pkgdir}"/opt/zextras/data/nginx/

  # systemd unit
  install -D -m 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  rm -rf "${pkgdir}/run"
}

postinst__apt() {
  if [ "$1" = "configure" ]; then
    # creating zextras group if it isn't already there
    if ! getent group zextras >/dev/null; then
      addgroup --system --force-badname --quiet zextras
    fi

    # creating zextras user if it isn't already there
    if ! getent passwd zextras >/dev/null; then
      adduser --system --force-badname --quiet \
        --ingroup zextras \
        --home /opt/zextras \
        --shell /bin/bash \
        zextras
      usermod -c "zextras user" zextras
    fi
  fi

  chown root:zextras /opt/zextras/common/sbin/nginx
  chmod 750 /opt/zextras/common/sbin/nginx
  # changing permission will reset capabilities so set it again
  setcap CAP_NET_BIND_SERVICE=+ep /opt/zextras/common/sbin/nginx

  chown zextras:zextras /opt/zextras/log
}

postinst__yum() {
  # creating zextras group if it isn't already there
  if ! getent group zextras >/dev/null; then
    groupadd --system zextras >/dev/null
  fi
  # creating zextras user if it isn't already there
  if ! getent passwd zextras >/dev/null; then
    useradd --system \
      -g zextras \
      -d /opt/zextras \
      --shell /bin/bash \
      zextras >/dev/null
    usermod -c "zextras user" zextras
  fi

  chown root:zextras /opt/zextras/common/sbin/nginx
  chmod 750 /opt/zextras/common/sbin/nginx
  # changing permission will reset capabilities so set it again
  setcap CAP_NET_BIND_SERVICE=+ep /opt/zextras/common/sbin/nginx

  chown zextras:zextras /opt/zextras/log
}
