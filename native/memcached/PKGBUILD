pkgname="carbonio-memcached"
pkgver="1.6.40" # renovate: datasource=github-tags depName=memcached/memcached
pkgrel="2"
pkgdesc="Distributed memory object caching system"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('BSD-3-Clause')
depends__apt=(
  "carbonio-libevent"
)
makedepends__apt=(
  "pkg-config"
)
depends__yum=(
  "carbonio-libevent"
)
makedepends__yum=(
  "pkgconfig"
)
section="utils"
priority="optional"
source=(
  "https://www.memcached.org/files/memcached-${pkgver}.tar.gz"
  "411-${pkgname}-setup.sh"
  "${pkgname}-intentions.json"
  "${pkgname}-policies.json"
  "${pkgname}-service-protocol.json"
  "${pkgname}-sidecar.service"
  "${pkgname}.hcl"
  "${pkgname}.service"
  "${pkgname}.sh"
)
sha256sums=('a3d360e9da2221a49bf9aae4e6880f2d44da6b2a2fae39b1911b9ca76488fbfd'
            '0cd06de30fe49858f1d0457ee5d8874bd8a688d110ce16d155e28d7804c4acaa'
            '0dac2d242e7b000e2fcff6c0dd1aac050e0005af37149d71eebe0dc6bf88f399'
            '8d25760595a59a5ca1f014093693caf911f5397ebce00752abcfa0db4a505c36'
            '50743f955abb8b550c5fd9384a7421e8b85631dfaa7e2d8e39259aa7fbcab7ee'
            '74cdeb0e6daa2c55f9f876ed0e37921636e86aa508af3f7abe643d4cef1b9215'
            'fbcb9ef7d28ac846a7411cc745a95a36c8e4c62095d6e42a40cb904174dd85f0'
            '34e48e8612fd4e197f59b76bdb3be954caacb5cc704dab90acfa4537bbded5fd'
            '7bdc7bd2cae5d4459906b2aa8299d16936ca8fb2cd5a125b31a2eb8cd1a795ea')

build() {

  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"

  cd "${srcdir}/memcached-${pkgver}"
  autoreconf -fiv
  ./configure --prefix=/opt/zextras/common
  make -j16
}

package() {
  cd "${srcdir}/memcached-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # systemd unit
  install -D -m 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  # start install service registration files for consul
  install -Dm644 "${srcdir}/411-${pkgname}-setup.sh" \
    "${pkgdir}/etc/zextras/pending-setups.d/411-${pkgname}-setup.sh"

  install -Dm755 "${srcdir}/${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"

  install -Dm644 "${srcdir}/${pkgname}-sidecar.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-sidecar.service"

  install -Dm644 "${srcdir}/${pkgname}-policies.json" \
    "${pkgdir}/etc/carbonio/memcached/service-discover/policies.json"

  install -Dm644 "${srcdir}/${pkgname}-intentions.json" \
    "${pkgdir}/etc/carbonio/memcached/service-discover/intentions.json"

  install -Dm644 "${srcdir}/${pkgname}-service-protocol.json" \
    "${pkgdir}/etc/carbonio/memcached/service-discover/service-protocol.json"

  install -Dm644 "${srcdir}/${pkgname}.hcl" \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
  # end install service registration files for consul
}

postinst() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-memcached-sidecar.service >/dev/null 2>&1 || :
  fi
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-memcached-sidecar.service >/dev/null 2>&1 || :
    systemctl stop carbonio-memcached-sidecar.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
