pkgname="carbonio-opendkim"
pkgver="2.11.0" # renovate: datasource=github-tags depName=trusteddomainproject/OpenDKIM
pkgrel="4"
pkgdesc="An open source implementation of the DKIM sender authentication system. Based on a fork of dkim-milter"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('BSD-3-Clause AND Sendmail')
depends__apt=(
  "carbonio-cyrus-sasl"
  "carbonio-bdb"
  "carbonio-libbsd"
  "carbonio-openldap"
  "carbonio-openssl"
  "zlib1g"
)
makedepends__apt=(
  "zlib1g-dev"
)
depends__yum=(
  "carbonio-cyrus-sasl"
  "carbonio-bdb"
  "carbonio-libbsd"
  "carbonio-openldap"
  "carbonio-openssl"
  "zlib"
)
makedepends__yum=(
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://github.com/trusteddomainproject/OpenDKIM/archive/14d54524e0a97d3fe9b80441907d7e356c9ded04.tar.gz"
  "${pkgname}.path"
  "${pkgname}.service"
  "${pkgname}-reload.service"
)
sha256sums=(
  'b9d033ac6ba4c14c42dd485e7dc8e03d6f415cb3d45a7fe8c020164636b9d23e'
  '90882afad11cc7b50468ca7c317dd563c904bc32423e80f521ebc0fbaa424d38'
  '141a1c9a16d6ca473ed46bfaab648e4dbacd2b539742302dee684af6e5e8efa4'
  '53a8cb3b86b80af379f050290de8b916eff110fec5d6b9621ab31d2fcc18b067'
)

build() {
  cd "${srcdir}/OpenDKIM-14d54524e0a97d3fe9b80441907d7e356c9ded04"

  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib"
  export CPPFLAGS="-I/opt/zextras/common/include"

  autoreconf -fiv
  ./configure --prefix=/opt/zextras/common \
    --enable-query_cache \
    --enable-poll \
    --enable-atps \
    --enable-rate_limit \
    --enable-replace_rules \
    --enable-resign \
    --enable-sender_macro \
    --enable-vbr \
    --enable-default_sender \
    --enable-rpath \
    --with-openssl=/opt/zextras/common \
    --with-milter=/opt/zextras/common \
    --with-libevent=/opt/zextras/common/lib \
    --with-sql-backend=/opt/zextras/common \
    --with-openldap=/opt/zextras/common \
    --with-sasl=/opt/zextras/common \
    --with-lmdb=/opt/zextras/common \
    --with-libcurl=/opt/zextras/common \
    --with-db=/opt/zextras/common

  make -j16
}

package() {
  cd "${srcdir}/OpenDKIM-14d54524e0a97d3fe9b80441907d7e356c9ded04"
  make DESTDIR="${pkgdir}" install

  # systemd units
  install -D -m 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -D -m 644 "${srcdir}/${pkgname}-reload.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-reload.service"
  install -D -m 644 "${srcdir}/${pkgname}.path" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.path"

  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.a
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}
