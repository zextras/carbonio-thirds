pkgname="carbonio-opendkim"
pkgver="2.10.3"
pkgrel="2"
pkgdesc="An open source implementation of the DKIM sender authentication system. Based on a fork of dkim-milter"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
depends__apt=(
  "carbonio-cyrus-sasl"
  "carbonio-bdb"
  "carbonio-libbsd"
  "carbonio-libmilter"
  "carbonio-openldap"
  "carbonio-openssl"
  "zlib1g"
)
makedepends__apt=(
  "autoconf"
  "zlib1g-dev"
)
depends__yum=(
  "carbonio-cyrus-sasl"
  "carbonio-bdb"
  "carbonio-libbsd"
  "carbonio-libmilter"
  "carbonio-openldap"
  "carbonio-openssl"
  "zlib"
)
makedepends__yum=(
  "autoconf"
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://downloads.sourceforge.net/project/opendkim/opendkim-${pkgver}.tar.gz"
  "openssl_1.1.0_compat.patch"
)
sha256sums=('43a0ba57bf942095fe159d0748d8933c6b1dd1117caf0273fa9a0003215e681b'
  '64d46209f4086c85eccfe09924856514e2150b79e54bad801a57def6e200d73e')

build() {
  cd "${srcdir}/opendkim-${pkgver}"
  mkdir -p lib/lib
  patch -Np1 -i "${srcdir}/openssl_1.1.0_compat.patch"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib"
  export CPPFLAGS="-I/opt/zextras/common/include"
  
  autoreconf -fiv
  ./configure --prefix=/opt/zextras/common \
    --enable-query_cache \
    --enable-poll \
    --enable-atps \
    --enable-rate_limit \
    --enable-replace_rules \
    --enable-resign \
    --enable-sender_macro \
    --enable-vbr \
    --enable-default_sender \
    --enable-rpath \
    --with-openssl=/opt/zextras/common \
    --with-milter=/opt/zextras/common \
    --with-libevent=/opt/zextras/common/lib \
    --with-sql-backend=/opt/zextras/common \
    --with-openldap=/opt/zextras/common \
    --with-sasl=/opt/zextras/common \
    --with-lmdb=/opt/zextras/common \
    --with-libcurl=/opt/zextras/common \
    --with-db=/opt/zextras/common
  grep -Rsni "\-Llib/lib" -l | xargs sed -i "s/Llib\/lib/L\/opt\/zextras\/common\/lib/g"
  make -j16
}

package() {
  cd "${srcdir}/opendkim-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.a
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}
