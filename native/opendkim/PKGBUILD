pkgname="carbonio-opendkim"
pkgver="2.11.0" # renovate: datasource=github-tags depName=trusteddomainproject/OpenDKIM
pkgrel="4"
pkgdesc="An open source implementation of the DKIM sender authentication system. Based on a fork of dkim-milter"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('BSD-3-Clause AND Sendmail')
depends__apt=(
  "carbonio-cyrus-sasl"
  "carbonio-bdb"
  "carbonio-libbsd"
  "carbonio-openldap"
  "carbonio-openssl"
  "zlib1g"
)
makedepends__apt=(
  "zlib1g-dev"
)
depends__yum=(
  "carbonio-cyrus-sasl"
  "carbonio-bdb"
  "carbonio-libbsd"
  "carbonio-openldap"
  "carbonio-openssl"
  "zlib"
)
makedepends__yum=(
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://github.com/trusteddomainproject/OpenDKIM/archive/14d54524e0a97d3fe9b80441907d7e356c9ded04.tar.gz"
  "${pkgname}.service"
)
sha256sums=(
  'b9d033ac6ba4c14c42dd485e7dc8e03d6f415cb3d45a7fe8c020164636b9d23e'
  '3b7ea80e4995bcb0aca73b102646719b000955a323f1de9d77548924c65b600f'
)

_postinst_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
    systemctl enable carbonio-opendkim.service &>/dev/null || :
  fi
}

_prerm_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable --now carbonio-opendkim.service &>/dev/null || :
  fi
}

build() {
  cd "${srcdir}/OpenDKIM-14d54524e0a97d3fe9b80441907d7e356c9ded04"

  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib"
  export CPPFLAGS="-I/opt/zextras/common/include"

  autoreconf -fiv
  ./configure --prefix=/opt/zextras/common \
    --enable-query_cache \
    --enable-poll \
    --enable-atps \
    --enable-rate_limit \
    --enable-replace_rules \
    --enable-resign \
    --enable-sender_macro \
    --enable-vbr \
    --enable-default_sender \
    --enable-rpath \
    --with-openssl=/opt/zextras/common \
    --with-milter=/opt/zextras/common \
    --with-libevent=/opt/zextras/common/lib \
    --with-sql-backend=/opt/zextras/common \
    --with-openldap=/opt/zextras/common \
    --with-sasl=/opt/zextras/common \
    --with-lmdb=/opt/zextras/common \
    --with-libcurl=/opt/zextras/common \
    --with-db=/opt/zextras/common

  make -j16
}

_package() {
  cd "${srcdir}/OpenDKIM-14d54524e0a97d3fe9b80441907d7e356c9ded04"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.a
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}

_package_systemd() {
  # systemd units
  install -Dm644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}

package() {
  _package
  _package_systemd
}

package__rocky_8() {
  _package
}

package__rocky_9() {
  _package
  _package_systemd
}

package__ubuntu_jammy() {
  _package
}

package__ubuntu_noble() {
  _package
  _package_systemd
}

postinst__rocky_9() {
  _postinst_systemd
}

postinst__ubuntu_noble() {
  _postinst_systemd
}

prerm__rocky_9() {
  _prerm_systemd
}

prerm__ubuntu_noble() {
  _prerm_systemd
}

postrm__rocky_9() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}

postrm__ubuntu_noble() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}
