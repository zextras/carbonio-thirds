pkgname="carbonio-cyrus-sasl"
pkgver="2.1.28" # renovate: datasource=github-releases depName=cyrusimap/cyrus-sasl
pkgrel="6"
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
depends__apt=(
  "carbonio-krb5"
  "carbonio-openssl"
  "zlib1g"
)
makedepends__apt=(
  "groff-base"
  "zlib1g-dev"
)
depends__yum=(
  "carbonio-krb5"
  "carbonio-openssl"
  "zlib"
)
makedepends__yum=(
  "groff-base"
  "zlib-devel"
)
section="utils"
priority="optional"
source=(
  "https://github.com/cyrusimap/cyrus-sasl/releases/download/cyrus-sasl-${pkgver}/cyrus-sasl-${pkgver}.tar.gz"
  "carbonio-auth.patch"
  "carbonio-saslauthd.service"
)
sha256sums=('7ccfc6abd01ed67c1a0924b353e526f1b766b21f42d4562ee635a8ebfc5bb38c'
  '2f624ccb8c5f351538bc990b9e72af18e8823fbc77a14bcd35cf66df4390cccc'
  '98aba7c3e748ed43c144ef8d0071b5a28696d93cfa7d30f6423ff017f1b898c7')

build() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"

  patch -Np1 -i "${srcdir}/carbonio-auth.patch"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib -L/opt/zextras/common/lib -lxml2"
  export CFLAGS="-D_REENTRANT -Wno-error -I/opt/zextras/common/include -I/opt/zextras/common/include/libxml2"
  export CPPFLAGS="-D_REENTRANT -Wno-error -I/opt/zextras/common/include"

  rm -f config/config.guess config/config.sub
  rm -f config/ltconfig config/ltmain.sh config/libtool.m4
  rm -f saslauthd/config/ltconfig
  rm -fr autom4te.cache
  libtoolize -f -c
  aclocal -I config -I m4
  automake -a -c -f
  autoheader
  autoconf -f
  sed -i.bak 's/-lRSAglue //' configure
  ./configure --prefix=/opt/zextras/common \
    --enable-static=no \
    --enable-shared \
    --enable-alwaystrue \
    --enable-checkapop \
    --enable-cram \
    --enable-digest \
    --disable-otp \
    --disable-srp \
    --disable-srp-setpass \
    --disable-krb4 \
    --with-gss_impl=mit \
    --enable-gssapi \
    --enable-auth-sasldb \
    --enable-plain \
    --enable-anon \
    --enable-login \
    --enable-ntlm \
    --disable-passdss \
    --enable-sql \
    --enable-zimbra \
    --without-pgsql \
    --with-mysql=/opt/zextras/common \
    --disable-macos-framework \
    --with-saslauthd=/run/carbonio \
    --with-plugindir=/opt/zextras/common/lib/sasl2 \
    --with-lib-subdir=lib \
    --with-openssl=/opt/zextras/common \
    --with-dblib=no \
    --with-configdir=/opt/zextras/conf/sasl2 \
    --with-devrandom=/dev/urandom

  # prevent excessive overlinking by libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -j16
}

package() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # systemd unit
  install -D -m 644 "${srcdir}/carbonio-saslauthd.service" \
    "${pkgdir}/usr/lib/systemd/system/carbonio-saslauthd.service"

  find "${pkgdir}" -name '*.a' -delete
  find "${pkgdir}" -name '*.la' -delete
}
