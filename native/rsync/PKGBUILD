pkgname="carbonio-rsync"
pkgver="3.2.3"
pkgrel="1"
pkgdesc="A fast and versatile file copying tool for remote and local files"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
depends__apt=(
  "libattr1"
  "libacl1"
  "libc6"
  "libpopt0"
)
makedepends__apt=(
  "libacl1-dev"
  "libattr1-dev"
  "liblz4-dev"
  "libxxhash-dev"
)
depends__yum=(
  "libacl"
  "libattr"
  "lz4"
  "popt"
  "xxhash"
)
makedepends__yum=(
  "libacl-devel"
  "libattr-devel"
  "lz4-devel"
  "popt-devel"
  "xxhash-devel"
)
section="utils"
priority="optional"
source=(
  "https://download.samba.org/pub/rsync/src/rsync-${pkgver}.tar.gz"
)
sha256sums=('becc3c504ceea499f4167a260040ccf4d9f2ef9499ad5683c179a697146ce50e')

build() {
  cd "${srcdir}/rsync-${pkgver}"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib -L/usr/lib/x86_64-linux-gnu -L/opt/zextras/common/lib"
  export CFLAGS="-I/opt/zextras/common/include -I/usr/include"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"

  ./configure \
    --prefix=/opt/zextras/common \
    --disable-debug \
    --disable-simd \
    --disable-zstd \
    --with-included-popt=no \
    --with-included-zlib=no \
    --localstatedir=/opt/zextras/data/tmp
  make -j16
}

package() {
  cd "${srcdir}/rsync-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/opt/zextras/common/share/man
  rm -rf "${pkgdir}"/opt/zextras/common/bin
  rm -rf "${pkgdir}"/opt/zextras/common/lib/*.la
}
