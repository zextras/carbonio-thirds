pkgname="carbonio-altermime"
pkgver="0.3.10"
pkgrel="7"
pkgdesc="MIME encoded email pack alteration tool"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com>"
url="https://github.com/zextras"
license=('BSD-2-Clause')
section="utils"
priority="optional"
source=(
  "https://pldaniels.com/altermime/altermime-${pkgver}.tar.gz"
  "altermime-0.3.10-fprintf-compiler-error.patch"
  "altermime-0.3.10-cflags.patch"
  "altermime-disclaimers.patch"
  "${pkgname}-config.service"
  "${pkgname}-config.timer"
)
sha256sums=(
  '8334da6b55d4a05dfe1492389dfe1f289953053a21773849b060d7c856ddc36e'
  '2d9dadc9ccf529f1a778a3f21b315c96aa1e9c81dfacc9e4e705725682e581a0'
  '21b1db5e5851453cd95cd8a69b9cd53cb5767a0e1408f232cb4a156e79731762'
  '406e0481a01ffe1491e3b937626e92022013fe815194d88b143c7361abfc3fe0'
  '31c664be391a980fd10557d6b475b1501da115c9d730d0a51032aead0c8f6906'
  '00954885634616f760e82e53e2fa6da4d6accbc44b4b74b2680e21ed7bfb0d87'
)

_package() {
  cd "${srcdir}/altermime-${pkgver}"
  install -d "${pkgdir}/opt/zextras/common/bin"
  install -m 755 altermime "${pkgdir}/opt/zextras/common/bin/"
}

_package_systemd() {
  # systemd unit
  install -Dm644 "${srcdir}/${pkgname}-config.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-config.service"
  install -Dm644 "${srcdir}/${pkgname}-config.timer" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-config.timer"
}

_postinst_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload &>/dev/null || :
    systemctl enable carbonio-altermime-config.service &>/dev/null || :
    systemctl enable carbonio-altermime-config.timer &>/dev/null || :
  fi
}

_prerm_systemd() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable --now carbonio-altermime-config.timer &>/dev/null || :
    systemctl --no-reload disable --now carbonio-altermime-config.service &>/dev/null || :
  fi
}

build() {
  cd "${srcdir}/altermime-${pkgver}"
  patch -Np0 -i "${srcdir}/altermime-0.3.10-fprintf-compiler-error.patch"
  patch -Np0 -i "${srcdir}/altermime-0.3.10-cflags.patch"
  patch -Np1 -i "${srcdir}/altermime-disclaimers.patch"

  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib"
  make
}

package() {
  _package
  _package_systemd
}

package__rocky_8() {
  _package
}

package__rocky_9() {
  _package
  _package_systemd
}

package__ubuntu_jammy() {
  _package
}

package__ubuntu_noble() {
  _package
  _package_systemd
}

postinst__rocky_9() {
  _postinst_systemd
}

postinst__ubuntu_noble() {
  _postinst_systemd
}

prerm__rocky_9() {
  _prerm_systemd
}

prerm__ubuntu_noble() {
  _prerm_systemd
}

postrm__rocky_9() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}

postrm__ubuntu_noble() {
  [ -d /run/systemd/system ] && systemctl daemon-reload &>/dev/null || :
}
